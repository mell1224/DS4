
-- 1. CREACIÓN DE BASE DE DATOS Y USO


IF DB_ID('PanaderiaDB') IS NULL
 BEGIN
    CREATE DATABASE PanaderiaDB;
 END;
GO
USE PanaderiaDB;
GO



-- 2. DDL - Definición de Estructura de Tablas


-- 2.1. Tabla de Usuarios
CREATE TABLE Usuarios (
    IdUsuario       INT PRIMARY KEY IDENTITY(1,1),
    NombreCompleto  NVARCHAR(100) NOT NULL,
    Telefono        NVARCHAR(20) NULL,
    Correo          NVARCHAR(100) NOT NULL UNIQUE,
    Contrasena      VARCHAR(100) NOT NULL, -- Asumiendo que es un hash
    Rol             NVARCHAR(20) NOT NULL DEFAULT 'Cliente',
    Activo          BIT NOT NULL DEFAULT 1,
    FechaRegistro   DATETIME NOT NULL DEFAULT GETDATE(),
    CONSTRAINT CK_Usuarios_Rol CHECK (Rol IN ('Administrador', 'Cliente'))
 );
 
-- 2.2. Tabla de Categorías
CREATE TABLE Categorias (
    IdCategoria INT PRIMARY KEY IDENTITY(1,1),
    Nombre      NVARCHAR(50) NOT NULL,
    Descripcion NVARCHAR(100) NULL,
    Activo      BIT NOT NULL DEFAULT 1
 );
 
-- 2.3. Tabla de Productos
CREATE TABLE Productos (
    IdProducto    INT PRIMARY KEY IDENTITY(1,1),
    IdCategoria   INT NOT NULL,
    Nombre        NVARCHAR(50) NOT NULL,
    Descripcion   NVARCHAR(200) NULL,
    PrecioVenta   DECIMAL(10,2) NOT NULL,
    ImagenURL     VARCHAR(255) NULL,
    Stock         INT NOT NULL DEFAULT 0,
    Activo        BIT NOT NULL DEFAULT 1,
    CONSTRAINT FK_Productos_Categorias 
        FOREIGN KEY (IdCategoria) REFERENCES Categorias(IdCategoria),
    CONSTRAINT CK_Productos_PrecioVenta CHECK (PrecioVenta >= 0),
    CONSTRAINT CK_Productos_Stock CHECK (Stock >= 0)
 );
 
-- 2.4. Tabla de Ingredientes (Inventario)
CREATE TABLE Ingredientes (
    IdIngrediente INT PRIMARY KEY IDENTITY(1,1),
    Nombre        NVARCHAR(50) NOT NULL,
    Cantidad      DECIMAL(10,2) NOT NULL,
    UnidadMedida  NVARCHAR(20) NOT NULL,
    CostoUnitario DECIMAL(10,2) NULL,
    FechaVencimiento DATE NULL,
    CONSTRAINT CK_Ingredientes_Cantidad CHECK (Cantidad >= 0),
    CONSTRAINT CK_Ingredientes_CostoUnitario 
        CHECK (CostoUnitario IS NULL OR CostoUnitario >= 0)
 );
 
-- 2.5. Tabla de Recetas (Cabecera)
CREATE TABLE Recetas (
    IdReceta      INT PRIMARY KEY IDENTITY(1,1),
    IdProducto    INT NOT NULL UNIQUE, 
    FechaCreacion DATETIME NOT NULL DEFAULT GETDATE(),
    CONSTRAINT FK_Recetas_Productos 
        FOREIGN KEY (IdProducto) REFERENCES Productos(IdProducto)
 );
 
-- 2.6. Tabla Detalle de Recetas
CREATE TABLE DetalleRecetas (
    IdDetalleReceta   INT PRIMARY KEY IDENTITY(1,1),
    IdReceta          INT NOT NULL,
    IdIngrediente     INT NOT NULL,
    CantidadRequerida DECIMAL(10,4) NOT NULL,
    CONSTRAINT FK_DetalleRecetas_Recetas 
        FOREIGN KEY (IdReceta) REFERENCES Recetas(IdReceta),
    CONSTRAINT FK_DetalleRecetas_Ingredientes 
        FOREIGN KEY (IdIngrediente) REFERENCES Ingredientes(IdIngrediente),
    CONSTRAINT UQ_DetalleRecetas UNIQUE (IdReceta, IdIngrediente),
    CONSTRAINT CK_DetalleRecetas_Cantidad CHECK (CantidadRequerida > 0)
 );
 
-- 2.7. Registro de Producción (log de horneadas)
CREATE TABLE RegistroProduccion (
    IdRegistro          INT PRIMARY KEY IDENTITY(1,1),
    IdProducto          INT NOT NULL,
    CantidadProducida   INT NOT NULL,
    FechaProduccion     DATETIME NOT NULL DEFAULT GETDATE(),
    Observaciones       NVARCHAR(500) NULL,
    CONSTRAINT FK_RegistroProduccion_Productos 
        FOREIGN KEY (IdProducto) REFERENCES Productos(IdProducto),
    CONSTRAINT CK_RegistroProduccion_Cantidad CHECK (CantidadProducida > 0)
 );
 
-- 2.8. Tabla de Pedidos (Cabecera)
CREATE TABLE Pedidos (
    IdPedido          INT PRIMARY KEY IDENTITY(1,1),
    IdUsuario         INT NOT NULL,
    FechaPedido       DATETIME NOT NULL DEFAULT GETDATE(),
    DireccionEntrega  NVARCHAR(255) NULL,
    Observaciones     NVARCHAR(500) NULL,
    MetodoPago        NVARCHAR(50) NOT NULL,
    Total             DECIMAL(10,2) NOT NULL,
    Estado            NVARCHAR(20) NOT NULL DEFAULT 'Pendiente',
    CONSTRAINT FK_Pedidos_Usuarios 
        FOREIGN KEY (IdUsuario) REFERENCES Usuarios(IdUsuario),
    CONSTRAINT CK_Pedidos_Total CHECK (Total >= 0),
    CONSTRAINT CK_Pedidos_Estado 
        CHECK (Estado IN ('Pendiente','En Preparacion','Completado','Cancelado'))
 );
 
-- 2.9. Detalle de Pedidos
CREATE TABLE DetallePedidos (
    IdDetalle      INT PRIMARY KEY IDENTITY(1,1),
    IdPedido       INT NOT NULL,
    IdProducto     INT NOT NULL,
    Cantidad       INT NOT NULL,
    PrecioUnitario DECIMAL(10,2) NOT NULL,
    CONSTRAINT FK_DetallePedidos_Pedidos 
        FOREIGN KEY (IdPedido)REFERENCES Pedidos(IdPedido),
    CONSTRAINT FK_DetallePedidos_Productos 
        FOREIGN KEY (IdProducto) REFERENCES Productos(IdProducto),
    CONSTRAINT CK_DetallePedidos_Cantidad CHECK (Cantidad > 0),
    CONSTRAINT CK_DetallePedidos_Precio CHECK (PrecioUnitario >= 0),
    CONSTRAINT UQ_DetallePedidos UNIQUE (IdPedido, IdProducto)
 );
GO


-- 3. SPs de Producción y Venta (Gestión de Stock)


-- 3.1. Registrar Producción (Descarga ingredientes, aumenta stock)
CREATE PROCEDURE SP_RegistrarProduccion
    @IdProducto        INT,
    @CantidadProducida INT,
    @Observaciones     NVARCHAR(500) = NULL
AS
BEGIN
    SET NOCOUNT ON;
 
    -- Validaciones
    IF @IdProducto IS NULL OR @IdProducto <= 0
        RETURN;
    IF @CantidadProducida IS NULL OR @CantidadProducida <= 0
        RETURN;
    IF NOT EXISTS (SELECT 1 FROM Productos WHERE IdProducto = @IdProducto AND Activo = 1)
        RETURN;
    IF NOT EXISTS (SELECT 1 FROM Recetas WHERE IdProducto = @IdProducto)
        RETURN;
 
    DECLARE @IdReceta INT;
    SELECT @IdReceta = IdReceta FROM Recetas WHERE IdProducto = @IdProducto;
 
    BEGIN TRANSACTION;
    BEGIN TRY
 
        -- PASO 1: Calcular los requerimientos totales (SET-BASED)
        SELECT 
            dr.IdIngrediente,
            dr.CantidadRequerida * @CantidadProducida AS CantidadConsumir
        INTO 
            #Requerimientos
        FROM 
            DetalleRecetas dr
        WHERE 
            dr.IdReceta = @IdReceta;
 
        -- PASO 2: Validación de Stock Insuficiente
        IF EXISTS (
            SELECT 1
            FROM #Requerimientos req
            JOIN Ingredientes i WITH (UPDLOCK) ON req.IdIngrediente = i.IdIngrediente
            WHERE i.Cantidad < req.CantidadConsumir
        )
        BEGIN
            RAISERROR('Stock insuficiente de uno o más ingredientes para la producción solicitada.', 16, 1);
            IF OBJECT_ID('tempdb..#Requerimientos') IS NOT NULL DROP TABLE #Requerimientos;
            RETURN;
        END;
 
        -- PASO 3: Descontar Inventario de Ingredientes (SET-BASED)
        UPDATE i
        SET i.Cantidad = i.Cantidad - req.CantidadConsumir
        FROM Ingredientes i
        JOIN #Requerimientos req ON i.IdIngrediente = req.IdIngrediente;
 
        -- PASO 4: Aumentar stock de producto terminado
        UPDATE Productos
          SET Stock = Stock + @CantidadProducida
        WHERE IdProducto = @IdProducto;
 
        -- PASO 5: Registrar en log de producción
        INSERT INTO RegistroProduccion (IdProducto, CantidadProducida, FechaProduccion, Observaciones)
        VALUES (@IdProducto, @CantidadProducida, GETDATE(), @Observaciones);
 
        -- Limpieza
        DROP TABLE #Requerimientos;
 
        COMMIT TRANSACTION;
        SELECT 1 AS Resultado, 'Producción registrada y stock actualizado correctamente.' AS Mensaje;
 
    END TRY
    BEGIN CATCH
        IF OBJECT_ID('tempdb..#Requerimientos') IS NOT NULL
            DROP TABLE #Requerimientos;
 
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION;
 
        SELECT 0 AS Resultado, 'Fallo en la Producción: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO
 
-- 3.2. Registrar Venta (Descuento de stock del producto terminado)
CREATE PROCEDURE SP_RegistrarVenta
    @IdProducto      INT,
    @CantidadVendida INT
AS
BEGIN
 
    SET NOCOUNT ON;
 
    IF @IdProducto IS NULL OR @IdProducto <= 0
        RETURN;
    IF @CantidadVendida IS NULL OR @CantidadVendida <= 0
        RETURN;
    IF NOT EXISTS (SELECT 1 FROM Productos WHERE IdProducto = @IdProducto AND Activo = 1)
        RETURN;
 
    BEGIN TRANSACTION;
    BEGIN TRY
        DECLARE @StockActual INT;
 
        SELECT @StockActual = Stock
        FROM Productos WITH (UPDLOCK)
        WHERE IdProducto = @IdProducto;
 
        IF @StockActual < @CantidadVendida
        BEGIN
            RAISERROR('Stock de producto terminado insuficiente para la venta.', 16, 1);
            RETURN;
        END;
 
        UPDATE Productos
        SET Stock = Stock - @CantidadVendida
        WHERE IdProducto = @IdProducto;
 
        COMMIT TRANSACTION;
        SELECT 1 AS Resultado, 'Venta descontada de stock correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION;
        SELECT 0 AS Resultado, 'Fallo al registrar la venta: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO


-- 4. SPs de Autenticación de Usuarios


-- 4.1. Insertar Usuario (Registro)
CREATE PROCEDURE SP_InsertarUsuario
    @NombreCompleto NVARCHAR(100),
    @Correo NVARCHAR(100),
    @Contrasena VARCHAR(100),
    @Telefono NVARCHAR(20) = NULL,
    @Rol NVARCHAR(20) = 'Cliente'
AS
BEGIN
    SET NOCOUNT ON;

    IF EXISTS (SELECT 1 FROM Usuarios WHERE Correo = @Correo)
    BEGIN
        SELECT 0 AS Resultado, 'Error: Ya existe una cuenta registrada con este correo.' AS Mensaje, NULL AS IdGenerado;
        RETURN;
    END

    IF @Rol NOT IN ('Administrador', 'Cliente')
    BEGIN
        SELECT 0 AS Resultado, 'Error: Rol no válido.' AS Mensaje, NULL AS IdGenerado;
        RETURN;
    END

    BEGIN TRY
        INSERT INTO Usuarios (NombreCompleto, Correo, Contrasena, Telefono, Rol, Activo)
        VALUES (@NombreCompleto, @Correo, @Contrasena, @Telefono, @Rol, 1);

        SELECT 1 AS Resultado, 'Usuario registrado correctamente.' AS Mensaje, SCOPE_IDENTITY() AS IdGenerado;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Fallo al registrar el usuario: ' + ERROR_MESSAGE() AS Mensaje, NULL AS IdGenerado;
    END CATCH
END;
GO

-- 4.2. Validar Login
CREATE PROCEDURE SP_ValidarLogin
    @Correo NVARCHAR(100),
    @Contrasena VARCHAR(100)
AS
BEGIN
    SET NOCOUNT ON;
    
    SELECT 
        IdUsuario, NombreCompleto, Correo, Rol
    FROM 
        Usuarios
    WHERE 
        Correo = @Correo 
        AND Contrasena = @Contrasena
        AND Activo = 1;
END;
GO


-- 5. SPs de Flujo de Pedidos (Venta)


-- 5.1. Crear Pedido Completo (Acción del botón "Confirmar y Pagar Pedido")
CREATE PROCEDURE SP_CrearPedido
    @IdUsuario INT,
    @DireccionEntrega NVARCHAR(255),
    @Observaciones NVARCHAR(500) = NULL,
    @MetodoPago NVARCHAR(50),
    @Total DECIMAL(10,2),
    @DetalleProductosXML XML 
AS
BEGIN
    SET NOCOUNT ON;
    
    -- Validaciones básicas
    IF @IdUsuario IS NULL OR @IdUsuario <= 0 OR NOT EXISTS (SELECT 1 FROM Usuarios WHERE IdUsuario = @IdUsuario AND Activo = 1) RETURN;
    IF @Total IS NULL OR @Total <= 0 RETURN;
    IF @DetalleProductosXML IS NULL OR @DetalleProductosXML.exist('/Productos/Producto') = 0 RETURN;
    
    CREATE TABLE #DetalleTemp (
        IdProducto INT,
        Cantidad INT,
        PrecioUnitario DECIMAL(10,2)
    );
    
    BEGIN TRANSACTION;
    BEGIN TRY
        -- 1. Parsear XML e insertar en tabla temporal
        INSERT INTO #DetalleTemp (IdProducto, Cantidad, PrecioUnitario)
        SELECT
            Tbl.Col.value('(IdProducto)[1]', 'INT'),
            Tbl.Col.value('(Cantidad)[1]', 'INT'),
            Tbl.Col.value('(PrecioUnitario)[1]', 'DECIMAL(10,2)')
        FROM @DetalleProductosXML.nodes('/Productos/Producto') AS Tbl(Col);

        -- 2. Validar que hay suficiente stock para todos los productos
        IF EXISTS (
            SELECT dt.IdProducto
            FROM #DetalleTemp dt
            JOIN Productos p ON dt.IdProducto = p.IdProducto
            WHERE p.Stock < dt.Cantidad OR p.Activo = 0
        )
        BEGIN
            RAISERROR('Stock insuficiente para uno o más productos o producto inactivo.', 16, 1);
            IF OBJECT_ID('tempdb..#DetalleTemp') IS NOT NULL DROP TABLE #DetalleTemp;
            RETURN;
        END;

        -- 3. Insertar la cabecera del pedido (tabla Pedidos)
        INSERT INTO Pedidos (IdUsuario, DireccionEntrega, Observaciones, MetodoPago, Total, Estado)
        VALUES (@IdUsuario, @DireccionEntrega, @Observaciones, @MetodoPago, @Total, 'Pendiente');

        DECLARE @NuevoIdPedido INT = SCOPE_IDENTITY();

        -- 4. Insertar el detalle del pedido (tabla DetallePedidos)
        INSERT INTO DetallePedidos (IdPedido, IdProducto, Cantidad, PrecioUnitario)
        SELECT @NuevoIdPedido, IdProducto, Cantidad, PrecioUnitario
        FROM #DetalleTemp;

        -- 5. Descontar stock de producto terminado llamando a SP_RegistrarVenta
        DECLARE @ProdId INT, @CantVendida INT;
        
        DECLARE StockCursor CURSOR LOCAL FOR
        SELECT IdProducto, Cantidad
        FROM #DetalleTemp;

        OPEN StockCursor;
        FETCH NEXT FROM StockCursor INTO @ProdId, @CantVendida;

        WHILE @@FETCH_STATUS = 0
        BEGIN
            -- Llama al SP de stock
            EXEC SP_RegistrarVenta @IdProducto = @ProdId, @CantidadVendida = @CantVendida;
            
            FETCH NEXT FROM StockCursor INTO @ProdId, @CantVendida;
        END;
        CLOSE StockCursor;
        DEALLOCATE StockCursor;

        -- Limpieza
        DROP TABLE #DetalleTemp;

        COMMIT TRANSACTION;
        SELECT 1 AS Resultado, 'Pedido registrado y stock descontado con éxito.' AS Mensaje, @NuevoIdPedido AS IdGenerado;

    END TRY
    BEGIN CATCH
        IF OBJECT_ID('tempdb..#DetalleTemp') IS NOT NULL DROP TABLE #DetalleTemp;
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        SELECT 0 AS Resultado, 'Fallo al procesar el pedido: ' + ERROR_MESSAGE() AS Mensaje, NULL AS IdGenerado;
    END CATCH
END;
GO

-- 5.2. Listar Pedidos (Vista Admin y Cliente Historial)
CREATE PROCEDURE SP_ListarPedidos
    @IdUsuario INT = NULL, -- Si es NULL, lista todos (Admin), si tiene ID, lista de un usuario (Cliente)
    @EstadoFiltro NVARCHAR(20) = 'Todos'
AS
BEGIN
    SET NOCOUNT ON;

    SELECT
        p.IdPedido,
        p.FechaPedido,
        u.NombreCompleto AS NombreCliente,
        p.Total,
        p.DireccionEntrega,
        p.Observaciones,
        p.MetodoPago,
        p.Estado
    FROM
        Pedidos p
    JOIN
        Usuarios u ON p.IdUsuario = u.IdUsuario
    WHERE
        (@IdUsuario IS NULL OR p.IdUsuario = @IdUsuario)
        AND (@EstadoFiltro = 'Todos' OR p.Estado = @EstadoFiltro)
    ORDER BY
        p.FechaPedido DESC;
END;
GO

-- 5.3. Obtener Detalle de un Pedido Específico
CREATE PROCEDURE SP_ObtenerDetallePedido
    @IdPedido INT
AS
BEGIN
    SET NOCOUNT ON;

    -- Cabecera del Pedido
    SELECT
        p.IdPedido, p.FechaPedido, p.Total, p.Estado, p.MetodoPago, p.DireccionEntrega, p.Observaciones, u.NombreCompleto AS NombreCliente
    FROM Pedidos p JOIN Usuarios u ON p.IdUsuario = u.IdUsuario
    WHERE p.IdPedido = @IdPedido;

    -- Detalle de Productos
    SELECT
        dp.Cantidad,
        dp.PrecioUnitario,
        (dp.Cantidad * dp.PrecioUnitario) AS Subtotal,
        prod.Nombre AS NombreProducto,
        prod.ImagenURL -- Para mostrar la imagen en el detalle
    FROM DetallePedidos dp JOIN Productos prod ON dp.IdProducto = prod.IdProducto
    WHERE dp.IdPedido = @IdPedido;
END;
GO

-- 5.4. Actualizar Estado del Pedido (Botones "Preparando >", "Completado >")
CREATE PROCEDURE SP_ActualizarEstadoPedido
    @IdPedido INT,
    @NuevoEstado NVARCHAR(20)
AS
BEGIN
    SET NOCOUNT ON;

    IF @IdPedido IS NULL OR @IdPedido <= 0 RETURN;
    IF @NuevoEstado NOT IN ('Pendiente','En Preparacion','Completado','Cancelado') RETURN;

    BEGIN TRY
        UPDATE Pedidos
        SET Estado = @NuevoEstado
        WHERE IdPedido = @IdPedido;

        SELECT 1 AS Resultado, 'Estado del pedido actualizado a ' + @NuevoEstado + ' correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Fallo al actualizar el estado: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO


-- 6. SPs de Gestión de Productos


-- 6.1. Listar Productos
CREATE PROCEDURE SP_ListarProductos
AS
BEGIN
    SET NOCOUNT ON;
    
    SELECT 
        p.IdProducto, p.IdCategoria, c.Nombre AS NombreCategoria, p.Nombre, p.Descripcion, p.PrecioVenta, p.ImagenURL, p.Stock, p.Activo
    FROM 
        Productos p JOIN Categorias c ON p.IdCategoria = c.IdCategoria
    ORDER BY 
        p.Nombre;
END;
GO

-- 6.2. Insertar Producto
CREATE PROCEDURE SP_InsertarProducto
    @IdCategoria INT, @Nombre NVARCHAR(50), @Descripcion NVARCHAR(200) = NULL, @PrecioVenta DECIMAL(10,2), @ImagenURL VARCHAR(255) = NULL, @Stock INT = 0
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdCategoria IS NULL OR @IdCategoria <= 0 OR NOT EXISTS (SELECT 1 FROM Categorias WHERE IdCategoria = @IdCategoria AND Activo = 1) RETURN;
    IF @Nombre IS NULL OR LTRIM(RTRIM(@Nombre)) = '' RETURN;
    IF @PrecioVenta IS NULL OR @PrecioVenta < 0 RETURN;
    BEGIN TRY
        INSERT INTO Productos (IdCategoria, Nombre, Descripcion, PrecioVenta, ImagenURL, Stock, Activo)
        VALUES (@IdCategoria, @Nombre, @Descripcion, @PrecioVenta, @ImagenURL, @Stock, 1);
        SELECT 1 AS Resultado, 'Producto insertado correctamente.' AS Mensaje, SCOPE_IDENTITY() AS IdGenerado;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Fallo al insertar el producto: ' + ERROR_MESSAGE() AS Mensaje, NULL AS IdGenerado;
    END CATCH
END;
GO

-- 6.3. Actualizar Producto
CREATE PROCEDURE SP_ActualizarProducto
    @IdProducto INT, @IdCategoria INT, @Nombre NVARCHAR(50), @Descripcion NVARCHAR(200) = NULL, @PrecioVenta DECIMAL(10,2), @ImagenURL VARCHAR(255) = NULL, @Activo BIT
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdProducto IS NULL OR @IdProducto <= 0 OR NOT EXISTS (SELECT 1 FROM Productos WHERE IdProducto = @IdProducto) RETURN;
    IF @IdCategoria IS NULL OR @IdCategoria <= 0 OR NOT EXISTS (SELECT 1 FROM Categorias WHERE IdCategoria = @IdCategoria) RETURN;
    IF @Nombre IS NULL OR LTRIM(RTRIM(@Nombre)) = '' RETURN;
    IF @PrecioVenta IS NULL OR @PrecioVenta < 0 RETURN;
    BEGIN TRY
        UPDATE Productos
        SET IdCategoria = @IdCategoria, Nombre = @Nombre, Descripcion = @Descripcion, PrecioVenta = @PrecioVenta, ImagenURL = @ImagenURL, Activo = @Activo
        WHERE IdProducto = @IdProducto;
        SELECT 1 AS Resultado, 'Producto actualizado correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Fallo al actualizar el producto: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO

-- 6.4. Eliminar Producto (Eliminación Lógica)
CREATE PROCEDURE SP_EliminarProducto
    @IdProducto INT
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdProducto IS NULL OR @IdProducto <= 0 OR NOT EXISTS (SELECT 1 FROM Productos WHERE IdProducto = @IdProducto) RETURN;
    BEGIN TRY
        UPDATE Productos SET Activo = 0 WHERE IdProducto = @IdProducto;
        SELECT 1 AS Resultado, 'Producto deshabilitado (eliminación lógica) correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Fallo al deshabilitar el producto: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO


-- 7. SPs de Gestión de Ingredientes (Inventario)


-- 7.1. Listar ingredientes
CREATE PROCEDURE SP_ListarIngredientes
AS
BEGIN
    SET NOCOUNT ON;
    SELECT IdIngrediente, Nombre, Cantidad, UnidadMedida, CostoUnitario, FechaVencimiento
    FROM Ingredientes
    ORDER BY Nombre;
END;
GO
 
-- 7.2. Insertar ingrediente
CREATE PROCEDURE SP_InsertarIngrediente
    @Nombre NVARCHAR(50), @Cantidad DECIMAL(10,2), @UnidadMedida NVARCHAR(20), @CostoUnitario DECIMAL(10,2), @FechaVencimiento DATE = NULL
AS
BEGIN
    SET NOCOUNT ON;
    IF @Nombre IS NULL OR LTRIM(RTRIM(@Nombre)) = '' RETURN;
    IF @Cantidad IS NULL OR @Cantidad < 0 RETURN;
    IF @CostoUnitario IS NOT NULL AND @CostoUnitario < 0 RETURN;
    BEGIN TRY
        INSERT INTO Ingredientes (Nombre, Cantidad, UnidadMedida, CostoUnitario, FechaVencimiento)
        VALUES (@Nombre, @Cantidad, @UnidadMedida, @CostoUnitario, @FechaVencimiento);
        SELECT 1 AS Resultado, 'Ingrediente insertado correctamente.' AS Mensaje, SCOPE_IDENTITY() AS IdGenerado;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Error al insertar el ingrediente: ' + ERROR_MESSAGE() AS Mensaje, NULL AS IdGenerado;
    END CATCH
END;
GO
 
-- 7.3. Actualizar ingrediente
CREATE PROCEDURE SP_ActualizarIngrediente
    @IdIngrediente INT, @Nombre NVARCHAR(50), @Cantidad DECIMAL(10,2), @UnidadMedida NVARCHAR(20), @CostoUnitario DECIMAL(10,2), @FechaVencimiento DATE = NULL
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdIngrediente IS NULL OR @IdIngrediente <= 0 RETURN;
    IF NOT EXISTS (SELECT 1 FROM Ingredientes WHERE IdIngrediente = @IdIngrediente) RETURN;
    IF @Nombre IS NULL OR LTRIM(RTRIM(@Nombre)) = '' RETURN;
    IF @Cantidad IS NULL OR @Cantidad < 0 RETURN;
    IF @CostoUnitario IS NOT NULL AND @CostoUnitario < 0 RETURN;
    BEGIN TRY
        UPDATE Ingredientes
        SET Nombre = @Nombre, Cantidad = @Cantidad, UnidadMedida = @UnidadMedida, CostoUnitario = @CostoUnitario, FechaVencimiento = @FechaVencimiento
        WHERE IdIngrediente = @IdIngrediente;
        SELECT 1 AS Resultado, 'Ingrediente actualizado correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Error al actualizar el ingrediente: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO
 
-- 7.4. Eliminar ingrediente
CREATE PROCEDURE SP_EliminarIngrediente
    @IdIngrediente INT
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdIngrediente IS NULL OR @IdIngrediente <= 0 RETURN;
    IF NOT EXISTS (SELECT 1 FROM Ingredientes WHERE IdIngrediente = @IdIngrediente) RETURN;
 
    IF EXISTS (SELECT 1 FROM DetalleRecetas WHERE IdIngrediente = @IdIngrediente)
    BEGIN
        SELECT 0 AS Resultado, 'Error: No se puede eliminar. El ingrediente está siendo usado en una o más recetas.' AS Mensaje;
        RETURN;
    END;
 
    BEGIN TRY
        DELETE FROM Ingredientes WHERE IdIngrediente = @IdIngrediente;
        SELECT 1 AS Resultado, 'Ingrediente eliminado correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        SELECT 0 AS Resultado, 'Error al eliminar el ingrediente: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO


-- 8. SPs de Gestión de Recetas


-- 8.1. Insertar receta completa (cabecera + detalle)
CREATE PROCEDURE SP_InsertarRecetaCompleta
    @IdProducto INT,
    @DetalleRecetasXML XML
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdProducto IS NULL OR @IdProducto <= 0 RETURN;
    IF NOT EXISTS (SELECT 1 FROM Productos WHERE IdProducto = @IdProducto AND Activo = 1) RETURN;
    IF EXISTS (SELECT 1 FROM Recetas WHERE IdProducto = @IdProducto) RETURN;
    IF @DetalleRecetasXML IS NULL OR @DetalleRecetasXML.exist('/Detalles/Detalle') = 0 RETURN;
 
    BEGIN TRANSACTION;
    BEGIN TRY
        INSERT INTO Recetas (IdProducto, FechaCreacion)
        VALUES (@IdProducto, GETDATE());
 
        DECLARE @NuevoIdReceta INT = SCOPE_IDENTITY();
 
        INSERT INTO DetalleRecetas (IdReceta, IdIngrediente, CantidadRequerida)
        SELECT @NuevoIdReceta, Tbl.Col.value('(IdIngrediente)[1]', 'INT'), Tbl.Col.value('(CantidadRequerida)[1]','DECIMAL(10,4)')
        FROM @DetalleRecetasXML.nodes('/Detalles/Detalle') AS Tbl(Col);
 
        COMMIT TRANSACTION;
        SELECT 1 AS Resultado, 'Receta registrada completamente con éxito.' AS Mensaje, @NuevoIdReceta AS IdReceta;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        SELECT 0 AS Resultado, 'Fallo al guardar la receta: ' + ERROR_MESSAGE() AS Mensaje, NULL AS IdReceta;
    END CATCH
END;
GO
 
-- 8.2. Obtener detalle de receta por producto
CREATE PROCEDURE SP_ObtenerDetalleRecetaPorProducto
    @IdProducto INT
AS
BEGIN
    SET NOCOUNT ON;
 
    SELECT
        dr.IdDetalleReceta, r.IdReceta, dr.IdIngrediente, i.Nombre AS NombreIngrediente, dr.CantidadRequerida, i.UnidadMedida
    FROM Recetas r
    JOIN DetalleRecetas dr ON r.IdReceta = dr.IdReceta
    JOIN Ingredientes i ON dr.IdIngrediente = i.IdIngrediente
    WHERE r.IdProducto = @IdProducto;
END;
GO
 
-- 8.3. Eliminar receta completa
CREATE PROCEDURE SP_EliminarReceta
    @IdProducto INT
AS
BEGIN
    SET NOCOUNT ON;
    IF @IdProducto IS NULL OR @IdProducto <= 0 RETURN;
 
    DECLARE @IdReceta INT;
    SELECT @IdReceta = IdReceta FROM Recetas WHERE IdProducto = @IdProducto;
 
    IF @IdReceta IS NULL RETURN;
 
    BEGIN TRANSACTION;
    BEGIN TRY
        DELETE FROM DetalleRecetas WHERE IdReceta = @IdReceta;
        DELETE FROM Recetas WHERE IdReceta = @IdReceta;
        COMMIT TRANSACTION;
        SELECT 1 AS Resultado, 'Receta eliminada correctamente.' AS Mensaje;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        SELECT 0 AS Resultado, 'Fallo al eliminar la receta: ' + ERROR_MESSAGE() AS Mensaje;
    END CATCH
END;
GO


CREATE PROCEDURE SP_ObtenerDatosProducto --Nuevo
    @IdProducto INT
AS
BEGIN
    SELECT IdProducto, Stock, PrecioVenta
    FROM Productos
    WHERE IdProducto = @IdProducto;
END
GO

CREATE PROCEDURE SP_ObtenerIngredientesReceta --Nuevo
    @IdProducto INT
AS
BEGIN
    SELECT 
        dr.IdIngrediente,
        i.Nombre AS NombreIngrediente,
        dr.CantidadRequerida,
        i.Cantidad AS CantidadActual,
        i.UnidadMedida
    FROM Recetas r
    INNER JOIN DetalleRecetas dr ON r.IdReceta = dr.IdReceta
    INNER JOIN Ingredientes i ON dr.IdIngrediente = i.IdIngrediente
    WHERE r.IdProducto = @IdProducto;
END
GO

CREATE PROCEDURE SP_InsertarDetalleReceta --Nuevo
    @IdReceta INT,
    @IdIngrediente INT,
    @CantidadRequerida DECIMAL(10,2)
AS
BEGIN
    INSERT INTO DetalleRecetas (IdReceta, IdIngrediente, CantidadRequerida)
    VALUES (@IdReceta, @IdIngrediente, @CantidadRequerida);
END;
GO

CREATE PROCEDURE SP_EliminarDetalleReceta --Nuevo
    @IdDetalleReceta INT
AS
BEGIN
    DELETE FROM DetalleRecetas WHERE IdDetalleReceta = @IdDetalleReceta;
END;
GOOO